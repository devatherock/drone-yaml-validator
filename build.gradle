plugins {
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

repositories {
    jcenter()
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.16'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.30'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.9.0'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2' // Added to fix security vulnerability
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.2'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['.']
            exclude(['**/test/**', 'gradle/**'])
        }
    }
}

compileGroovy {
    groovyOptions.configurationScript = file('gradle/config.groovy')
}

ext.jacoco = [
    exclusions: [
        'YamlValidator$_validateYamlFiles_closure1*'
    ],
    coverageThresholds: [
        'YamlValidator': [
            'BRANCH': 0.67,  // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'COMPLEXITY': 0.66, // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'INSTRUCTION': 0.93,
            'LINE': 0.94
        ]
    ]
]
apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/checks.gradle'