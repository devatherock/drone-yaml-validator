plugins {
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.diffplug.spotless' version '6.25.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    def spockVersion = '2.3-groovy-3.0'

    // Groovy version should match the version used within scriptjar
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.23'

    // Build dependency versions should match the ones used within the script
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.3'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.9.0'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'org.spockframework', name: 'spock-junit4', version: spockVersion // For Junit @Rule to work in Spock 2
    testImplementation group: 'junit', name: 'junit', version: '4.13.2' // Added to fix security vulnerability
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.4'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['.']
            exclude([
                '**/test/**',
                'gradle/**',
                'bin/**'
            ])
        }
    }
}

compileGroovy {
    groovyOptions.configurationScript = file('gradle/config.groovy')
}

test {
    useJUnitPlatform()
}

ext.jacoco = [
    exclusions: [
        'YamlValidator$_validateYamlFiles_closure1*'
    ],
    coverageThresholds: [
        'YamlValidator': [
            'BRANCH': 0.67,  // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'COMPLEXITY': 0.66, // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'INSTRUCTION': 0.93,
            'LINE': 0.90
        ],
        'TagIgnoringConstruct': [
            'INSTRUCTION': 0.98,
            'LINE': 0.75
        ]
    ]
]
apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/checks.gradle'